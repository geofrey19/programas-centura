.head 0 +  Application Description: Resizing and repositioning child windows

Developed for SQLWindows version 5.0.x
This source can be loaded into CTD 1.x and will work too
Be careful not to save in CTD format, this might make the
file unusable for SQLWindows

Provided "as is", no guarantees. May be distributed freely,
as long as the code remains unchanged. Private and commercial 
use allowed. If you make any fixes or enhancements, please
let me know 

Joachim Meyer (JoeM@basys-bremen.de)
BASYS EDV-Systeme GmbH
.head 1 -  Outline Version - 4.0.26
.head 1 +  Design-time Settings
.data VIEWINFO
0000: 6F00000001000000 FFFF01000D004347 5458566965775374 6174650400800000
0020: 0000000000040100 002C000000020000 0003000000FFFFFF FFFFFFFFFFFCFFFF
0040: FFE9FFFFFF000000 0000000000180300 00C3010000010000 0000000000010000
0060: 000F4170706C6963 6174696F6E497465 6D02000000075769 6E646F7773076672
0080: 6D4D61696E
.enddata
.head 2 -  Outline Window State: Maximized
.head 2 +  Outline Window Location and Size
.data VIEWINFO
0000: 6600040003001B00 0000000000000000 0000F828BC1A0500 1D00FFFF4D61696E
0020: 0020000100000000 0000000000E91E80 0A0000DF00FFFF56 61726961626C6573
0040: 0029000100000000 0000000000F828BC 1A00008600FFFF49 6E7465726E616C20
0060: 46756E6374696F6E 73001E0001000000 000000000000F828 BC1A0000F400FFFF
0080: 436C617373657300
.enddata
.data VIEWSIZE
0000: 8800
.enddata
.head 3 -  Left:   -0.013"
.head 3 -  Top:    0.0"
.head 3 -  Width:  8.013"
.head 3 -  Height: 4.969"
.head 2 +  Options Box Location
.data VIEWINFO
0000: D4180909B80B1A00
.enddata
.data VIEWSIZE
0000: 0800
.enddata
.head 3 -  Visible? Yes
.head 3 -  Left:   4.15"
.head 3 -  Top:    1.885"
.head 3 -  Width:  3.8"
.head 3 -  Height: 2.073"
.head 2 +  Class Editor Location
.head 3 -  Visible? No
.head 3 -  Left:   0.575"
.head 3 -  Top:    0.094"
.head 3 -  Width:  5.063"
.head 3 -  Height: 2.719"
.head 2 +  Tool Palette Location
.head 3 -  Visible? No
.head 3 -  Left:   10.3"
.head 3 -  Top:    1.073"
.head 2 -  Fully Qualified External References? No
.head 2 -  Reject Multiple Window Instances? Yes
.head 2 -  Enable Runtime Checks Of External References? Yes
.head 2 -  Use Release 4.0 Scope Rules? No
.head 1 -  Libraries
.head 1 +  Global Declarations
.head 2 +  Window Defaults
.head 3 +  Tool Bar
.head 4 -  Display Style? Etched
.head 4 -  Font Name: MS Sans Serif
.head 4 -  Font Size: 8
.head 4 -  Font Enhancement: None
.head 4 -  Text Color: System Default
.head 4 -  Background Color: System Default
.head 3 +  Form Window
.head 4 -  Display Style? Etched
.head 4 -  Font Name: MS Sans Serif
.head 4 -  Font Size: 8
.head 4 -  Font Enhancement: None
.head 4 -  Text Color: System Default
.head 4 -  Background Color: System Default
.head 3 +  Dialog Box
.head 4 -  Display Style? Etched
.head 4 -  Font Name: MS Sans Serif
.head 4 -  Font Size: 8
.head 4 -  Font Enhancement: None
.head 4 -  Text Color: System Default
.head 4 -  Background Color: System Default
.head 3 +  Top Level Table Window
.head 4 -  Font Name: MS Sans Serif
.head 4 -  Font Size: 8
.head 4 -  Font Enhancement: None
.head 4 -  Text Color: System Default
.head 4 -  Background Color: System Default
.head 3 +  Data Field
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Multiline Field
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Spin Field
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Background Text
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Pushbutton
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 3 +  Radio Button
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Check Box
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Option Button
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 3 +  Group Box
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Child Table Window
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  List Box
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Combo Box
.head 4 -  Font Name: Use Parent
.head 4 -  Font Size: Use Parent
.head 4 -  Font Enhancement: Use Parent
.head 4 -  Text Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Line
.head 4 -  Line Color: Use Parent
.head 3 +  Frame
.head 4 -  Border Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 3 +  Picture
.head 4 -  Border Color: Use Parent
.head 4 -  Background Color: Use Parent
.head 2 -  Formats
.head 2 -  External Functions
.head 2 +  Constants
.data CCDATA
0000: 3000000000000000 0000000000000000 00000000
.enddata
.data CCSIZE
0000: 1400
.enddata
.head 3 +  System
.head 4 -  ! Application messages:
.head 4 -  Number: PM_FORM_RESIZED		= SAM_User + 50	! if 50 conflicts with your app, choose another number
.head 3 -  User
.head 2 -  Resources
.head 2 -  Variables
.head 2 -  Internal Functions
.head 2 -  Named Menus
.head 2 +  Class Definitions
.head 3 -  !
.head 3 -  ! resize and align:
.head 3 -  !
.head 3 +  ! What is "resizing"?
.head 4 -  ! "Resizing" means, that a control (like table windows, listboxes, etc.) changes its
  size whenever the form changes its size. The control's borders maintain a fixed 
  distance to the right or bottom border of the parent window.
.head 3 +  ! What is "aligning"?
.head 4 -  ! "Aligning" means, that a control changes its relative position to the top or the 
  bottom (or both) of the parent form, whenever its parent form's size changes. The
  controls maintain a fixed distance to the right or bottom border of the parent window. 
.head 3 +  ! How can I achieve this?
.head 4 -  ! First you have to derive a class (for example a child table) from cControlParent and
  drop it on a cFormWindow. Then call the Init-functions of the control during creation
  of the form. If you want a minimum size or position for the control, call SetMinInfo()
  additionally. That's all
.head 3 -  !
.head 3 +  General Window Class: cControlParent
.head 4 -  Description: Use this as a common parent for all controls that need to 
handle resizing of the parent form.
.head 4 -  Derived From
.head 4 -  Class Variables
.head 4 +  Instance Variables
.head 5 -  ! internal variables:
.head 5 -  Boolean: m_ResizeRight
.head 5 -  Boolean: m_ResizeBottom
.head 5 -  Boolean: m_AlignWidth
.head 5 -  Boolean: m_AlignHeight
.head 5 -  Number: m_DX	! distance to the right border of the parent form
.head 5 -  Number: m_DY	! distance to the bottom border of the parent form
.head 5 -  Number: m_MinX	! minimum x-position or minimum-width (depends on fStyle)
.head 5 -  Number: m_MinY	! minimum y-position or minimum-height (depends on fStyle)
.head 5 -  Number: m_LastW	! store last width
.head 5 -  Number: m_LastH	! store last height
.head 4 +  Functions
.head 5 -  !
.head 5 -  ! Initialize the control:
.head 5 +  Function: InitResize
.head 6 -  Description: Call InitResize( bResizeRight, bResizeBottom )
<bResizeRight>	TRUE: resize width, when parent's width changes
<bResizeBottom>	TRUE: resize height, when parent's height changes

Use this function to define the resize parameters.
Depending on the parameters the control maintains it's size
relatively to the right/bottom border of the parent form
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  Boolean: ResizeRight
.head 7 -  Boolean: ResizeBottom
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  ! helpers:
.head 7 -  Number: x
.head 7 -  Number: y
.head 7 -  Number: w
.head 7 -  Number: h
.head 6 +  Actions
.head 7 -  ! set Style according to parameters:
.head 7 -  Set m_ResizeRight = ResizeRight
.head 7 -  Set m_ResizeBottom = ResizeBottom
.head 7 -  ! where am I and who's my parent form?
.head 7 -  Call SalGetWindowLoc( hWndItem, x, y )
.head 7 -  Call SalGetWindowSize( SalParentWindow( hWndItem ), w, h )
.head 7 -  ! store the distance from right/bottom border for later use:
.head 7 -  Set m_DX = w - x
.head 7 -  Set m_DY = h - y
.head 5 +  Function: InitAlign
.head 6 -  Description: Call InitAlign( bAlignWidth, bAlignHeight )
<bAlignWidth>	TRUE: align x-position, when parent's width changes
<bAlignHeight>	TRUE: align y-position, when parent's height changes

Use this function to define the align parameters.
Depending on the parameters the control maintains it's position
relatively to the right/bottom border of the parent form
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  Boolean: AlignWidth
.head 7 -  Boolean: AlignHeight
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  ! helpers:
.head 7 -  Number: x
.head 7 -  Number: y
.head 7 -  Number: w
.head 7 -  Number: h
.head 7 -  Number: pw
.head 7 -  Number: ph
.head 6 +  Actions
.head 7 -  ! set Style according to parameters:
.head 7 -  Set m_AlignWidth = AlignWidth
.head 7 -  Set m_AlignHeight = AlignHeight
.head 7 -  ! where am I, who's my parent form and how big am I?
.head 7 -  Call SalGetWindowLoc( hWndItem, x, y )
.head 7 -  Call SalGetWindowSize( SalParentWindow( hWndItem ), pw, ph )
.head 7 -  Call SalGetWindowSize( hWndItem, w, h )
.head 7 -  ! store distance from borders:
.head 7 -  Set m_DX = pw - (x + w)
.head 7 -  Set m_DY = ph - (y + h)
.head 7 -  ! set minimum values to default:
.head 7 -  Set m_MinX = 0.2
.head 7 -  Set m_MinY = 0.2
.head 5 -  !
.head 5 -  ! set the minimum X/Y positions or minimum width/height:
.head 5 +  Function: SetMinInfo
.head 6 -  Description: Call SetMinInfo( nMinX, nMinY )
if initialized via InitResize, MinX/MinY are minimum positions
if initialized via InitAlign, MinX/MinY are minimum sizes
.head 6 -  Returns
.head 6 +  Parameters
.head 7 -  Number: MinX
.head 7 -  Number: MinY
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 -  Set m_MinX = MinX
.head 7 -  Set m_MinY = MinY
.head 5 -  !
.head 5 -  ! internal:
.head 5 +  Function: __OnFormResize
.head 6 -  Description:
.head 6 -  Returns
.head 6 -  Parameters
.head 6 -  Static Variables
.head 6 +  Local variables
.head 7 -  ! and again we need some helpers:
.head 7 -  Number: w
.head 7 -  Number: h
.head 7 -  Number: NewX
.head 7 -  Number: NewY
.head 7 -  Number: NewW
.head 7 -  Number: NewH
.head 6 +  Actions
.head 7 -  ! At first we need the current size of our parent window:
.head 7 -  Call SalGetWindowSize( SalParentWindow( hWndItem ), w, h )
.head 7 -  ! I that size didn't change, we don't need to do anything:
.head 7 +  If (w != m_LastW) or (h != m_LastH)
.head 8 -  ! save current width/height of parent form for the next time:
.head 8 -  Set m_LastW = w
.head 8 -  Set m_LastH = h
.head 8 -  ! Is this a "glue" control?
.head 8 +  If m_ResizeRight or m_ResizeBottom
.head 9 -  ! where am I?
.head 9 -  Call SalGetWindowLoc( hWndItem, NewX, NewY )
.head 9 -  ! if glued to right border, then calculate new x-position
.head 9 +  If m_ResizeRight
.head 10 -  Set NewX = w - m_DX
.head 10 +  If (m_MinX > 0) and (NewX < m_MinX)
.head 11 -  Set NewX = m_MinX
.head 9 -  ! if glued to bottomt border, then calculate new y-position
.head 9 +  If m_ResizeBottom
.head 10 -  Set NewY = h - m_DY
.head 10 +  If (m_MinY > 0) and (NewY < m_MinY)
.head 11 -  Set NewY = m_MinY
.head 9 -  ! now set the new position and repaint myself:
.head 9 -  Call SalSetWindowLoc( hWndItem, NewX, NewY )
.head 9 -  Call SalInvalidateWindow( hWndItem )
.head 8 -  ! is this a "resize" control?
.head 8 +  If m_AlignWidth or m_AlignHeight
.head 9 -  ! where am I and how big am I?
.head 9 -  Call SalGetWindowSize( hWndItem, NewW, NewH )
.head 9 -  Call SalGetWindowLoc( hWndItem, NewX, NewY )
.head 9 -  ! if width aligned, then calculate new width
.head 9 +  If m_AlignWidth
.head 10 -  Set NewW = __Max( m_MinX, w - NewX - m_DX )
.head 9 -  ! if height aligned, then calculate new height
.head 9 +  If m_AlignHeight
.head 10 -  Set NewH = __Max( m_MinY, h - NewY - m_DY )
.head 9 -  ! now set the new width and repaint myself:
.head 9 -  Call SalSetWindowSize( hWndItem, NewW, NewH )
.head 9 -  Call SalInvalidateWindow( hWndItem )
.head 8 -  ! we're done.
.head 5 +  Function: __Max
.head 6 -  Description: returns the higher one of two given numbers
.head 6 +  Returns
.head 7 -  Number:
.head 6 +  Parameters
.head 7 -  Number: N1
.head 7 -  Number: N2
.head 6 -  Static Variables
.head 6 -  Local variables
.head 6 +  Actions
.head 7 +  If N1 > N2
.head 8 -  Return N1
.head 7 -  Return N2
.head 4 +  Message Actions
.head 5 +  On PM_FORM_RESIZED
.head 6 -  ! This Message handler get's called by the parent form (if the
  form is derived from cFormWindow) every time, the size of
  the parent form has changed.
.head 6 -  Call __OnFormResize()
.head 3 +  Form Window Class: cFormWindow
.head 4 -  Title:
.head 4 -  Icon File:
.head 4 -  Accesories Enabled? Class Default
.head 4 -  Visible? Class Default
.head 4 -  Display Settings
.head 5 -  Display Style? Class Default
.head 5 -  Visible at Design time? Yes
.head 5 -  Automatically Created at Runtime? Class Default
.head 5 -  Initial State: Class Default
.head 5 -  Maximizable? Class Default
.head 5 -  Minimizable? Class Default
.head 5 -  System Menu? Class Default
.head 5 -  Resizable? Class Default
.head 5 -  Window Location and Size
.head 6 -  Left:  
.head 6 -  Top:   
.head 6 -  Width:  Class Default
.head 6 -  Width Editable? Class Default
.head 6 -  Height: Class Default
.head 6 -  Height Editable? Class Default
.head 5 -  Form Size
.head 6 -  Width:  Class Default
.head 6 -  Height: Class Default
.head 6 -  Number of Pages: Class Default
.head 5 -  Font Name: Class Default
.head 5 -  Font Size: Class Default
.head 5 -  Font Enhancement: Class Default
.head 5 -  Text Color: Class Default
.head 5 -  Background Color: Class Default
.head 4 -  Next Class Child Key: 0
.head 4 -  List in Tool Palette? Yes
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Description: Class to be used as the parent of a resizable form.
It handles the WM_SIZE (0x0005) message and informs all the child
controls when the parent size has changed. It also avoids
unnecessary resizing during the creation process.

Important:
  SAM_Create and SAM_CreateComplete are implemented which means
  you have to place SalSendClassMessage( SAM_..., wParam, lParam )
  in overriden message actions.
.head 4 -  Derived From
.head 4 -  Menu
.head 4 +  Tool Bar
.head 5 -  Display Settings
.head 6 -  Display Style? Class Default
.head 6 -  Location? Class Default
.head 6 -  Visible? Class Default
.head 6 -  Size: Class Default
.head 6 -  Size Editable? Class Default
.head 6 -  Font Name: Class Default
.head 6 -  Font Size: Class Default
.head 6 -  Font Enhancement: Class Default
.head 6 -  Text Color: Class Default
.head 6 -  Background Color: Class Default
.head 5 -  Contents
.head 4 -  Contents
.head 4 -  Class Variables
.head 4 +  Instance Variables
.head 5 -  ! needed to suppress resizing during creation of the parent form:
.head 5 -  Boolean: __bCreating
.head 4 -  Functions
.head 4 +  Message Actions
.head 5 +  On 0x0005
.head 6 -  ! Windows message: WM_SIZE = 0x0005. You can create a constant if you like
.head 6 -  ! This message handler doesn't do anything while the parent form
  is inside the creation process.
.head 6 +  If not __bCreating
.head 7 -  ! inform all child controls about the resizing of the parent form:
.head 7 -  Call SalSendMsgToChildren( hWndForm, PM_FORM_RESIZED, 0, 0 )
.head 5 +  On SAM_Create
.head 6 -  ! Set the flag that's used in the WM_SIZE message handler to avoid
unnecessary resizing during the creation process. The form is not yet visible.
.head 6 -  Set __bCreating = TRUE
.head 5 +  On SAM_CreateComplete
.head 6 -  ! Reset the flag that's used in the WM_SIZE message handler to avoid
unnecessary resizing during the creation process. The form is visible now.
.head 6 -  Set __bCreating = FALSE
.head 3 -  !
.head 3 -  ! Examples: 
.head 3 -  !
.head 3 -  ! The following example controls are derived from cControlParent. None
  of them implements any actions, they just inherit the resizing capability.
  If you need other controls to be resized/aligned feel free to derive
  additional classes.
.head 3 +  Child Table Class: cChildTable
.head 4 -  Display Settings
.head 5 -  Window Location and Size
.head 6 -  Left:  
.head 6 -  Top:   
.head 6 -  Width:  Class Default
.head 6 -  Width Editable? Class Default
.head 6 -  Height: Class Default
.head 6 -  Height Editable? Class Default
.head 5 -  Visible? Class Default
.head 5 -  Font Name: Class Default
.head 5 -  Font Size: Class Default
.head 5 -  Font Enhancement: Class Default
.head 5 -  Text Color: Class Default
.head 5 -  Background Color: Class Default
.head 5 -  View: Class Default
.head 5 -  Allow Row Sizing? Class Default
.head 5 -  Lines Per Row: Class Default
.head 4 -  Memory Settings
.head 5 -  Maximum Rows in Memory: Class Default
.head 5 -  Discardable? Class Default
.head 4 -  Next Class Child Key: 0
.head 4 -  List in Tool Palette? Yes
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Description:
.head 4 +  Derived From
.head 5 -  Class: cControlParent
.head 4 -  Contents
.head 4 -  Class Variables
.head 4 -  Instance Variables
.head 4 -  Functions
.head 4 -  Message Actions
.head 3 +  List Box Class: cListBox
.head 4 -  Window Location and Size
.head 5 -  Left:  
.head 5 -  Top:   
.head 5 -  Width:  Class Default
.head 5 -  Width Editable? Class Default
.head 5 -  Height: Class Default
.head 5 -  Height Editable? Class Default
.head 4 -  Visible? Class Default
.head 4 -  Multiple selection? Class Default
.head 4 -  Sorted? Class Default
.head 4 -  Vertical Scroll? Class Default
.head 4 -  Font Name: Class Default
.head 4 -  Font Size: Class Default
.head 4 -  Font Enhancement: Class Default
.head 4 -  Text Color: Class Default
.head 4 -  Background Color: Class Default
.head 4 -  List in Tool Palette? Yes
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Description:
.head 4 +  Derived From
.head 5 -  Class: cControlParent
.head 4 -  Class Variables
.head 4 -  Instance Variables
.head 4 -  Functions
.head 4 -  List Initialization
.head 4 -  Message Actions
.head 3 +  Pushbutton Class: cPushButton
.head 4 -  Title:
.head 4 -  Window Location and Size
.head 5 -  Left:  
.head 5 -  Top:   
.head 5 -  Width:  Class Default
.head 5 -  Width Editable? Class Default
.head 5 -  Height: Class Default
.head 5 -  Height Editable? Class Default
.head 4 -  Visible? Class Default
.head 4 -  Keyboard Accelerator: Class Default
.head 4 -  Font Name: Class Default
.head 4 -  Font Size: Class Default
.head 4 -  Font Enhancement: Class Default
.head 4 -  Picture File Name:
.head 4 -  Picture Transparent Color: Class Default
.head 4 -  Image Style: Class Default
.head 4 -  Text Color: Class Default
.head 4 -  Background Color: Class Default
.head 4 -  List in Tool Palette? Yes
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Description:
.head 4 +  Derived From
.head 5 -  Class: cControlParent
.head 4 -  Class Variables
.head 4 -  Instance Variables
.head 4 -  Functions
.head 4 -  Message Actions
.head 3 +  Check Box Class: cCheckBox
.head 4 -  Title:
.head 4 -  Window Location and Size
.head 5 -  Left:  
.head 5 -  Top:   
.head 5 -  Width:  Class Default
.head 5 -  Width Editable? Class Default
.head 5 -  Height: Class Default
.head 5 -  Height Editable? Class Default
.head 4 -  Visible? Class Default
.head 4 -  Font Name: Class Default
.head 4 -  Font Size: Class Default
.head 4 -  Font Enhancement: Class Default
.head 4 -  Text Color: Class Default
.head 4 -  Background Color: Class Default
.head 4 -  List in Tool Palette? Yes
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Description:
.head 4 +  Derived From
.head 5 -  Class: cControlParent
.head 4 -  Class Variables
.head 4 -  Instance Variables
.head 4 -  Functions
.head 4 -  Message Actions
.head 2 +  Default Classes
.head 3 -  MDI Window: cBaseMDI
.head 3 -  Form Window:
.head 3 -  Dialog Box:
.head 3 -  Table Window:
.head 3 -  Quest Window:
.head 3 -  Data Field:
.head 3 -  Spin Field:
.head 3 -  Multiline Field: cQuickMLField
.head 3 -  Pushbutton: cPushButton
.head 3 -  Radio Button:
.head 3 -  Option Button:
.head 3 -  Check Box:
.head 3 -  Child Table: cChildTable
.head 3 -  Quest Child Window: cQuickDatabase
.head 3 -  List Box: cListBox
.head 3 -  Combo Box:
.head 3 -  Picture: cQuickPicture
.head 3 -  Vertical Scroll Bar:
.head 3 -  Horizontal Scroll Bar:
.head 3 -  Column:
.head 3 -  Background Text:
.head 3 -  Group Box:
.head 3 -  Line:
.head 3 -  Frame:
.head 3 -  Custom Control: cQuickGraph
.head 2 -  Application Actions
.head 1 +  ! Version History:
.head 2 -  ! v1.01	primary release
.head 2 -  ! v1.02	980708 Joachim Meyer
	additional comments
.head 1 -  !
.head 1 -  ! Example form:
.head 1 -  !
.head 1 +  Form Window: frmMain
.head 2 -  Class: cFormWindow
.head 2 -  Property Template:
.head 2 -  Class DLL Name:
.head 2 -  Title: Resizing example
.head 2 -  Icon File:
.head 2 -  Accesories Enabled? Class Default
.head 2 -  Visible? Class Default
.head 2 -  Display Settings
.head 3 -  Display Style? Class Default
.head 3 -  Visible at Design time? No
.head 3 -  Automatically Created at Runtime? Class Default
.head 3 -  Initial State: Class Default
.head 3 -  Maximizable? Class Default
.head 3 -  Minimizable? Class Default
.head 3 -  System Menu? Class Default
.head 3 -  Resizable? Class Default
.head 3 -  Window Location and Size
.head 4 -  Left:   0.288"
.head 4 -  Top:    0.896"
.head 4 -  Width:  8.133"
.head 4 -  Width Editable? Class Default
.head 4 -  Height: 3.714"
.head 4 -  Height Editable? Class Default
.head 3 -  Form Size
.head 4 -  Width:  Class Default
.head 4 -  Height: Class Default
.head 4 -  Number of Pages: Class Default
.head 3 -  Font Name: Class Default
.head 3 -  Font Size: Class Default
.head 3 -  Font Enhancement: Class Default
.head 3 -  Text Color: Class Default
.head 3 -  Background Color: Class Default
.head 2 -  Description: This is an example demonstrating the use of the resizing/aligning controls.
.head 2 -  Named Menus
.head 2 -  Menu
.head 2 +  Tool Bar
.head 3 -  Display Settings
.head 4 -  Display Style? Default
.head 4 -  Location? Top
.head 4 -  Visible? Yes
.head 4 -  Size: Default
.head 4 -  Size Editable? Yes
.head 4 -  Font Name: Default
.head 4 -  Font Size: Default
.head 4 -  Font Enhancement: Default
.head 4 -  Text Color: Default
.head 4 -  Background Color: Default
.head 3 -  Contents
.head 2 +  Contents
.head 3 +  Check Box: cbCheckbox
.head 4 -  Class Child Ref Key: 0
.head 4 -  Class ChildKey: 0
.head 4 -  Class: cCheckBox
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Title: Checkbox
.head 4 -  Window Location and Size
.head 5 -  Left:   6.283"
.head 5 -  Top:    0.06"
.head 5 -  Width:  1.2"
.head 5 -  Width Editable? Class Default
.head 5 -  Height: 0.25"
.head 5 -  Height Editable? Class Default
.head 4 -  Visible? Class Default
.head 4 -  Font Name: Class Default
.head 4 -  Font Size: Class Default
.head 4 -  Font Enhancement: Class Default
.head 4 -  Text Color: Class Default
.head 4 -  Background Color: Class Default
.head 4 -  Message Actions
.head 3 +  List Box: lbListbox
.head 4 -  Class Child Ref Key: 0
.head 4 -  Class ChildKey: 0
.head 4 -  Class: cListBox
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Window Location and Size
.head 5 -  Left:   0.083"
.head 5 -  Top:    0.381"
.head 5 -  Width:  2.6"
.head 5 -  Width Editable? Class Default
.head 5 -  Height: 2.345"
.head 5 -  Height Editable? Class Default
.head 4 -  Visible? Class Default
.head 4 -  Multiple selection? Class Default
.head 4 -  Sorted? Class Default
.head 4 -  Vertical Scroll? Class Default
.head 4 -  Font Name: Class Default
.head 4 -  Font Size: Class Default
.head 4 -  Font Enhancement: Class Default
.head 4 -  Text Color: Class Default
.head 4 -  Background Color: Class Default
.head 4 -  Horizontal Scroll? Class Default
.head 4 -  List Initialization
.head 4 -  Message Actions
.head 3 +  Child Table: tblTable
.head 4 -  Class Child Ref Key: 0
.head 4 -  Class ChildKey: 0
.head 4 -  Class: cChildTable
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Display Settings
.head 5 -  Window Location and Size
.head 6 -  Left:   2.833"
.head 6 -  Top:    0.381"
.head 6 -  Width:  4.95"
.head 6 -  Width Editable? Class Default
.head 6 -  Height: 2.25"
.head 6 -  Height Editable? Class Default
.head 5 -  Visible? Class Default
.head 5 -  Font Name: Class Default
.head 5 -  Font Size: Class Default
.head 5 -  Font Enhancement: Class Default
.head 5 -  Text Color: Class Default
.head 5 -  Background Color: Class Default
.head 5 -  View: Class Default
.head 5 -  Allow Row Sizing? Class Default
.head 5 -  Lines Per Row: Class Default
.head 4 -  Memory Settings
.head 5 -  Maximum Rows in Memory: Class Default
.head 5 -  Discardable? Class Default
.head 4 +  Contents
.head 5 +  Column: col1
.head 6 -  Class Child Ref Key: 0
.head 6 -  Class ChildKey: 0
.head 6 -  Class:
.head 6 -  Property Template:
.head 6 -  Class DLL Name:
.head 6 -  Title: This is the
left column
.head 6 -  Visible? Yes
.head 6 -  Editable? Yes
.head 6 -  Maximum Data Length: Default
.head 6 -  Data Type: String
.head 6 -  Justify: Left
.head 6 -  Width:  2.0"
.head 6 -  Width Editable? Yes
.head 6 -  Format: Unformatted
.head 6 -  Country: Default
.head 6 -  Input Mask: Unformatted
.head 6 -  Cell Options
.head 7 -  Cell Type? Standard
.head 7 -  Multiline Cell? No
.head 7 -  Cell DropDownList
.head 8 -  Sorted? Yes
.head 8 -  Vertical Scroll? Yes
.head 8 -  Auto Drop Down? No
.head 8 -  Allow Text Editing? Yes
.head 7 -  Cell CheckBox
.head 8 -  Check Value:
.head 8 -  Uncheck Value:
.head 8 -  Ignore Case? Yes
.head 6 -  List Values
.head 6 -  Message Actions
.head 5 +  Column: col2
.head 6 -  Class Child Ref Key: 0
.head 6 -  Class ChildKey: 0
.head 6 -  Class:
.head 6 -  Property Template:
.head 6 -  Class DLL Name:
.head 6 -  Title: This is the
right column
.head 6 -  Visible? Yes
.head 6 -  Editable? Yes
.head 6 -  Maximum Data Length: Default
.head 6 -  Data Type: String
.head 6 -  Justify: Left
.head 6 -  Width:  2.083"
.head 6 -  Width Editable? Yes
.head 6 -  Format: Unformatted
.head 6 -  Country: Default
.head 6 -  Input Mask: Unformatted
.head 6 -  Cell Options
.head 7 -  Cell Type? Standard
.head 7 -  Multiline Cell? No
.head 7 -  Cell DropDownList
.head 8 -  Sorted? Yes
.head 8 -  Vertical Scroll? Yes
.head 8 -  Auto Drop Down? No
.head 8 -  Allow Text Editing? Yes
.head 7 -  Cell CheckBox
.head 8 -  Check Value:
.head 8 -  Uncheck Value:
.head 8 -  Ignore Case? Yes
.head 6 -  List Values
.head 6 -  Message Actions
.head 4 -  Functions
.head 4 -  Window Variables
.head 4 -  Message Actions
.head 3 +  Pushbutton: pbClose
.head 4 -  Class Child Ref Key: 0
.head 4 -  Class ChildKey: 0
.head 4 -  Class: cPushButton
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Title: &Close
.head 4 -  Window Location and Size
.head 5 -  Left:   6.4"
.head 5 -  Top:    2.821"
.head 5 -  Width:  1.2"
.head 5 -  Width Editable? Class Default
.head 5 -  Height: 0.298"
.head 5 -  Height Editable? Class Default
.head 4 -  Visible? Class Default
.head 4 -  Keyboard Accelerator: Esc
.head 4 -  Font Name: Class Default
.head 4 -  Font Size: Class Default
.head 4 -  Font Enhancement: Class Default
.head 4 -  Picture File Name:
.head 4 -  Picture Transparent Color: Class Default
.head 4 -  Image Style: Class Default
.head 4 -  Text Color: Class Default
.head 4 -  Background Color: Class Default
.head 4 +  Message Actions
.head 5 +  On SAM_Click
.head 6 -  Call SalDestroyWindow( hWndForm )
.head 3 +  Pushbutton: pbRemark
.head 4 -  Class Child Ref Key: 0
.head 4 -  Class ChildKey: 0
.head 4 -  Class: cPushButton
.head 4 -  Property Template:
.head 4 -  Class DLL Name:
.head 4 -  Title: &Remark...
.head 4 -  Window Location and Size
.head 5 -  Left:   0.183"
.head 5 -  Top:    2.845"
.head 5 -  Width:  1.15"
.head 5 -  Width Editable? Class Default
.head 5 -  Height: 0.298"
.head 5 -  Height Editable? Class Default
.head 4 -  Visible? Class Default
.head 4 -  Keyboard Accelerator: Class Default
.head 4 -  Font Name: Class Default
.head 4 -  Font Size: Class Default
.head 4 -  Font Enhancement: Class Default
.head 4 -  Picture File Name:
.head 4 -  Picture Transparent Color: Class Default
.head 4 -  Image Style: Class Default
.head 4 -  Text Color: Class Default
.head 4 -  Background Color: Class Default
.head 4 +  Message Actions
.head 5 +  On SAM_Click
.head 6 -  Call SalMessageBox( 'Did you wonder why the listbox does not resize
smoothly? That\'s because it handles it\'s height
in a way that let\'s the last entry always be
completely visible. The height does not change
in pixels but in item heights.
It\'s not a bug, it\'s a feature!', 'Remark', MB_IconExclamation | MB_Ok )
.head 2 +  Functions
.head 3 +  Function: CreateTestData
.head 4 -  Description: Fill listbox and child table with some stupid test data
.head 4 -  Returns
.head 4 +  Parameters
.head 5 -  Number: MaxEntries
.head 4 -  Static Variables
.head 4 +  Local variables
.head 5 -  Number: i
.head 4 +  Actions
.head 5 -  Set i = 1
.head 5 +  While i <= MaxEntries
.head 6 -  Call SalListAdd( lbListbox, 'List item #' || SalNumberToStrX( i, 0 ) )
.head 6 -  Call SalTblInsertRow( tblTable, TBL_MaxRow )
.head 6 -  Set tblTable.col1 = 'Table cell #' || SalNumberToStrX( i, 0 ) || '.1'
.head 6 -  Set tblTable.col2 = 'Cell #' || SalNumberToStrX( i, 0 ) || '.2'
.head 6 -  Set i = i + 1
.head 5 -  Call SalTblSetFlagsAnyRows( tblTable, ROW_New, FALSE, 0, 0 )
.head 2 -  Window Parameters
.head 2 -  Window Variables
.head 2 +  Message Actions
.head 3 +  On SAM_Create
.head 4 -  ! call parent first:
.head 4 -  Call SalSendClassMessage( SAM_Create, wParam, lParam )
.head 4 -  !
.head 4 -  ! define the child controls:
.head 4 -  !
.head 4 -  ! define Checkbox to stick to the right border and set minimum x-/y-position:
.head 4 -  Call cbCheckbox.InitResize( TRUE, FALSE )
.head 4 -  Call cbCheckbox.SetMinInfo( 1, 0 )
.head 4 -  ! define listbox to be resized in height only with minimum height of 1:
.head 4 -  Call lbListbox.InitAlign( FALSE, TRUE )
.head 4 -  Call lbListbox.SetMinInfo( 0, 1 )
.head 4 -  ! define table to be resized in height and width with minimum height=1, minimum width=1:
.head 4 -  Call tblTable.InitAlign( TRUE, TRUE )
.head 4 -  Call tblTable.SetMinInfo( 1, 1 )
.head 4 -  ! define close button to stick to both the right and bottom border and set minimum x-/y-position:
.head 4 -  Call pbClose.InitResize( TRUE, TRUE )
.head 4 -  Call pbClose.SetMinInfo( 4, 1.25 )
.head 4 -  ! define remark button to stick to the bottom border only and set minimum y-position:
.head 4 -  Call pbRemark.InitResize( FALSE, TRUE )
.head 4 -  Call pbRemark.SetMinInfo( 0, 1.25 )
.head 4 -  !
.head 4 -  ! put some test data into listbox and table:
.head 4 -  Call CreateTestData( 50 )
